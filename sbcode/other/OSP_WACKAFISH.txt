VAR B, T1, T2, DT, NF=0, H=0, M=0, TX, TY, TS
ACLS : XSCREEN 3 : DISPLAY 1 : T2=MILLISEC
GFILL 0, 0, 319, 239, #BLUE
REPEAT
 T1 = T2 : T2 = MILLISEC: DT = T2 - T1
 TOUCH OUT TS, TX, TY : B = BUTTON()
 IF NF <= 0 THEN CRE_F : NF = 150 + RND(500) ELSE DEC NF, DT
 CALL SPRITE : LOCATE 0, 0 : PRINT "WACK-A-FISH: HITS " + STR$(H) + " MISSES " + STR$(M) + " " * 10 :VSYNC
UNTIL (B AND #B) != 0 OR M > 5
END
DEF CRE_F
 VAR ID
 SPSET 415 OUT ID
 SPOFS ID, RND(304), RND(224)
 SPVAR ID, 0, RND(2500) + 1000
 SPFUNC ID, "UPD_F"
END
DEF UPD_F
 VAR TL = SPVAR(CALLIDX, 0), X, Y
 DEC TL, DT : SPVAR CALLIDX, 0, TL
 SPOFS CALLIDX OUT X, Y
 IF TS != 0 AND X <= TX AND X + 16 >= TX AND Y <= TY AND Y + 16 >= TY THEN INC H : SPCLR CALLIDX : BEEP 7 ELSEIF TL <= 0 THEN INC M : SPCLR CALLIDX : BEEP 14
END
