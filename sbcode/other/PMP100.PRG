DATA "        Petit Movie Player"
DATA "      2017/05/01 VER.1.0.0"
DATA "              REI SOFTWARE"
DATA ""

VAR SPEEDTEST%=0
VAR USEPCM%=((EXTFEATURE AND 1)==1)
'USEPCM%=0

WHILE TRUE
 READ MES$
 IF MES$=="" THEN BREAK
 INC TITLE$," "*(50-LEN(MES$))+MES$
WEND

DIM FL$[0]
DIM FL2$[0]
VAR DUMMY$
VAR FOLDER$

@TITLE
BGMSTOP
IF EXTFEATURE AND 1 THEN PCMSTOP
ACLS

@SELECT_FOLDER

'FOR TOOL BUTTON
FOLDER$=""
GOTO @SELECT_FILE

CLS
LOCATE 0,0:COLOR 12,0
PRINT TITLE$:PRINT 
MES$="FREEMEMORY:"+STR$(FREEMEM)
PRINT " "*(50-LEN(MES$))+MES$
MES$="PETITCOM VERSION:"+HEX$(VERSION)
PRINT " "*(50-LEN(MES$))+MES$
IF USEPCM% THEN
 MES$="SOUND UNIT ON "
ELSE
 MES$="SOUND UNIT OFF"
ENDIF
PRINT " "*(50-LEN(MES$))+MES$

COLOR 15,0
PRINT "SELECT FOLDER"
PRINT ""
FILES "//",FL$:DUMMY$=SHIFT(FL$)
I%=SELECT(FL$,"NO FOLDER")
IF I%<0 THEN GOTO @TITLE
FOLDER$=FL$[I%]

@SELECT_FILE
CLS
LOCATE 0,0:COLOR 12,0
PRINT TITLE$:PRINT 
MES$="FREEMEMORY:"+STR$(FREEMEM)
PRINT " "*(50-LEN(MES$))+MES$
MES$="PETITCOM VERSION:"+HEX$(VERSION)
PRINT " "*(50-LEN(MES$))+MES$
IF USEPCM% THEN
 MES$="SOUND UNIT: ON"
ELSE
 MES$="SOUND UNIT:OFF"
ENDIF
PRINT " "*(50-LEN(MES$))+MES$

COLOR 15,0
IF FOLDER$!="" THEN
 PRINT "FOLDER: " + FOLDER$
 PRINT ""
ENDIF
PRINT "SELECT PMV FILE"
PRINT ""
FILES "DAT:"+FOLDER$,FL$
WHILE LEN(FL2$)>0:DUMMY$=SHIFT(FL2$):WEND
FOR I%=0 TO LEN(FL$)-1
 IF RIGHT$(FL$[I%],7)=="000.PMV" THEN
  PUSH FL2$,MID$(FL$[I%],1,LEN(FL$[I%])-1)
 ENDIF
NEXT

I%=SELECT(FL2$,"NO PMV FILE")
IF I%<0 THEN GOTO @SELECT_FOLDER
FILE$=FL2$[I%]

FILEPATH$=FOLDER$+FILE$



'DISPLAY INITIALIZATION
ACLS:XSCREEN 0,17,0
BGMSTOP 0
BGMSTOP 1
GPAGE 0,4
GFILL 0,0,400,240,RGB(0,0,0,0)
DIM VC%
FOR VC%=0 TO 15
 X%=(VC% AND  3)*80
 Y%=(VC% AND 12)*12
 SPCLR VC%
 SPSET VC%,X%,Y%,80,48,0
 SPSCALE VC%,5,5
 SPHOME VC%,40,24
 SPOFS VC%,200,120,1
 SPHIDE VC%
NEXT
SPCLR 16
SPSET 16,0,0,320,192,0
SPSCALE 16,5/4,5/4
SPOFS 16,0,0,1023
SPHIDE 16

'FILENAME EXTRACT
I%=INSTR(FILEPATH$,"000")
DIM FH$=MID$(FILEPATH$,0,I%)
'LOADIND
DIM D%[0]
DIM DL%,DC%
DIM G%[80*48]:FILL G%,1
DIM W%[32768]
DIM WL%=16384-1
DIM CT%,CSIZE%
DIM TT$="",TT2$="",ATH$=""
DIM FI%=0
DIM F$

F$="DAT:"+FH$+FORMAT$("%03D",FI%)+".PMV"
LOAD F$,D%,FALSE
DL%=LEN(D%):DC%=0

'READ INFO
REPEAT
 CT%    =  D%[DC%]      AND &HF
 CSIZE% = (D%[DC%] >> 4)AND &HFFF
 IF CT%==0 THEN
 ELSEIF CT%==1 THEN
  MES$=""
  FOR I%=1 TO CSIZE%-1
   MES$=MES$+CHR$(D%[DC%+I%]AND&HFFFF)
   MES$=MES$+CHR$(D%[DC%+I%]>>16)
  NEXT
  I%=INSTR(0,MES$,CHR$(0))
  IF I%<0 THEN
   TT$ =MES$
   TT2$=""
   ATH$=""
  ELSE
   TT$ =MID$(MES$,0,I%)
   J%=INSTR(I%+1,MES$,CHR$(0))
   IF J%<0 THEN
    TT2$=MID$(MES$,I%+1,LEN(MES$)-I%-1)
    ATH$=""
   ELSE
    TT2$=MID$(MES$,I%+1,J%-I%-1)
    I%=INSTR(J%+1,MES$,CHR$(0))
    IF I%<0 THEN
     ATH$=MID$(MES$,J%+1,LEN(MES$)-J%-1)
    ELSE
     ATH$=MID$(MES$,J%+1,I%-J%-1)
    ENDIF
   ENDIF
  ENDIF
 ELSEIF CT%==2 THEN
  MES$=""
  FOR I%=1 TO CSIZE%-1
   MES$=MES$+CHR$(D%[DC%+I%]AND&HFFFF)
   MES$=MES$+CHR$(D%[DC%+I%]>>16)
  NEXT
  DIALOG MES$
 ELSE
  BREAK
 ENDIF
 DC%=DC%+CSIZE%
UNTIL FALSE



IF !SPEEDTEST% THEN GOTO @SKIP_PERF
''''''''''''''''
'MEASURE PERFORMANCE
SPSHOW 16
MCNT%=MAINCNT+1
WHILE MAINCNT<MCNT%:WEND
REPEAT
 CT%=D%[DC%]
 CSIZE%=(CT%>>4) AND &HFFF:CT%=CT%AND&HF
 IF CT%==8 THEN
  'GOSUB @ADPCM2DEC
 ELSEIF CT%==9 THEN
  GOSUB @IFRAMEDEC
  GLOAD (VC% AND 3)*80,(VC% AND 12)*12,80,48,G%,1,1
  VC%=VC%+1
 ELSEIF CT%==10 THEN
  GOSUB @PFRAMEDEC
  GLOAD (VC% AND 3)*80,(VC% AND 12)*12,80,48,G%,1,1
  VC%=VC%+1
 ENDIF
 DC%=DC%+CSIZE%
 IF DC%>=DL% THEN
  DC%=0:K%=K%+1 IF K%>10 THEN BREAK
  CONTINUE
  'FILEREAD
  FI%=FI%+1
  F$="DAT:"+FH$+FORMAT$("%03D",FI%)+".PMV"
  IF !CHKFILE(F$) THEN BREAK
  LOAD F$,D%,FALSE
  DL%=LEN(D%):DC%=0
 ENDIF
UNTIL FALSE
?MAINCNT-MCNT%
END
'318
@SKIP_PERF
''''''''''''''''















'SHOW TITLE
LOCATE 1,12,-128
PRINT TT$
LOCATE 1,13,-128
PRINT TT2$
LOCATE 49-LEN(ATH$),16,-128:PRINT ATH$;
MCNT%=MAINCNT+60*3

'SOUND INIT
IF USEPCM% THEN
 FILL W%,0
ELSE
 FILL W%,128
 WAVSETA 224,127,127,127,127,W%
 WAVSETA 225,127,127,127,127,W%
 BGMSET 128,"@D-24@224O4T480C1&C1"
 BGMSET 129,"@D-24@225O4T480C1&C1"
ENDIF
PD%=0:PD8%=0:SA%=127

'PREREAD SOUND1
SPP%=0
REPEAT
 CT%=D%[DC%]
 CSIZE%=(CT%>>4) AND &HFFF:CT%=CT%AND&HF
'PINFO
 IF CT%==8 THEN
  VNCNT%=MAINCNT+1000
  IF USEPCM% THEN
   J%=SPP%*16384
   GOSUB @ADPCM2DEC_PCM
  ELSE
   GOSUB @ADPCM2DEC
  ENDIF
  DC%=DC%+CSIZE%
  BREAK
 ENDIF
 DC%=DC%+CSIZE%
UNTIL FALSE

'FRAME VAR INIT
VC%=0

'PREREAD MOVIE
REPEAT
 CT%=D%[DC%]
 CSIZE%=(CT%>>4) AND &HFFF:CT%=CT%AND&HF
'PINFO
 IF CT%==9 THEN
  GOSUB @IFRAMEDEC
  GLOAD (VC% AND 3)*80,(VC% AND 12)*12,80,48,G%,1,1
  VC%=VC%+1
  DC%=DC%+CSIZE%
  IF VC%>=15 THEN BREAK
 ELSEIF CT%==10 THEN
  GOSUB @PFRAMEDEC
  GLOAD (VC% AND 3)*80,(VC% AND 12)*12,80,48,G%,1,1
  VC%=VC%+1
  DC%=DC%+CSIZE%
  IF VC%>=15 THEN BREAK
 ELSEIF CT%==8 THEN
  BREAK
 ELSE 
  PRINT "ERROR":STOP
 ENDIF
UNTIL FALSE

'PREREAD SOUND2
SPP%=1
REPEAT
 CT%=D%[DC%]
 CSIZE%=(CT%>>4) AND &HFFF:CT%=CT%AND&HF
'PINFO
 IF CT%==8 THEN
  VNCNT%=MAINCNT+1000
  IF USEPCM% THEN
   J%=SPP%*16384
   GOSUB @ADPCM2DEC_PCM
  ELSE
   GOSUB @ADPCM2DEC
  ENDIF
  DC%=DC%+CSIZE%
  BREAK
 ENDIF
 DC%=DC%+CSIZE%
UNTIL FALSE

'WAIT TITLE
WHILE MAINCNT<MCNT%
WEND
'START
CLS
SPP%=0
MCNT%=MAINCNT+1
VNCNT%=MCNT%+4
MVC%=0:VPP%=0
SNCNT%=MCNT%+60
WHILE MAINCNT<MCNT%
WEND
IF USEPCM% THEN
 PCMSTREAM W%,16364
ELSE
 BGMPLAY SPP%,128+SPP%
ENDIF
SPP%=(SPP%+1)AND 1

'MAIN LOOP
REPEAT
'PINFO

IF MAINCNT>=SNCNT% THEN
 IF USEPCM% THEN ELSE BGMPLAY SPP%,128+SPP%
 SPP%=(SPP%+1)AND 1
 REPEAT
  SNCNT%=SNCNT%+60
  IF DC%>=DL% THEN
   'FILEREAD
   FI%=FI%+1
   F$="DAT:"+FH$+FORMAT$("%03D",FI%)+".PMV"
   IF !CHKFILE(F$) THEN
    J%=SPP%*16384
    FILL W%,0,J%,16384
    GOTO @OUTRO
   ENDIF
   LOAD F$,D%,FALSE
   DL%=LEN(D%):DC%=0
   GOSUB @ROTVBUF
  ENDIF
  'FIND SOUND CHUNK
  REPEAT
   CT%=D%[DC%]
   CSIZE%=(CT%>>4) AND &HFFF:CT%=CT%AND&HF
   IF CT%==8 THEN BREAK
   DC%=DC%+CSIZE%
  UNTIL FALSE
  IF MAINCNT<SNCNT% THEN BREAK
  DC%=DC%+CSIZE%
 UNTIL FALSE
 'DECODE SOUND
 IF USEPCM% THEN
  IF SPP%==0 THEN
   'JITTER COMPENSATION
   DF%=0
   IF PCMPOS>16384+1024 THEN DF%=-1
   IF PCMPOS<16384      THEN DF%= 1
   SNCNT%=SNCNT%+DF%
   VNCNT%=VNCNT%+DF%
  ENDIF
  J%=SPP%*16384
  GOSUB @ADPCM2DEC_PCM
 ELSE
  GOSUB @ADPCM2DEC
 ENDIF
 DC%=DC%+CSIZE%
 CONTINUE
ELSEIF MAINCNT>=VNCNT% THEN
 GOSUB @ROTVBUF
 CONTINUE
ENDIF
'VIDEO WRITE
IF VC%<MVC%+16 THEN
 IF DC%>=DL% THEN
  GLOAD (VC% AND 3)*80,(VC% AND 12)*12,80,48,G%,1,1
  VC%=VC%+1
  CONTINUE
 ELSE
  CT%=D%[DC%]
  CSIZE%=(CT%>>4) AND &HFFF:CT%=CT%AND&HF
  IF CT%==9 THEN
   'DRAW IFRAME
   GOSUB @IFRAMEDEC
   IF VC%>MVC%+7 THEN GOSUB @IFRAMEDEC
   GLOAD (VC% AND 3)*80,(VC% AND 12)*12,80,48,G%,1,1
   VC%=VC%+1
   DC%=DC%+CSIZE%
   CONTINUE
  ELSEIF CT%==10 THEN
   'DRAW PFRAME
   GOSUB @PFRAMEDEC
   IF VC%>MVC%+3 THEN GOSUB @PFRAMEDEC
   GLOAD (VC% AND 3)*80,(VC% AND 12)*12,80,48,G%,1,1
   VC%=VC%+1
   DC%=DC%+CSIZE%
   CONTINUE
  ENDIF
 ENDIF
ENDIF

'EFFECT
B%=BUTTON(0)
IF B%==0 THEN
 BCNT%=-1
ELSEIF MAINCNT>BCNT% THEN
 IF BCNT%<0 THEN
  BCNT%=MAINCNT+30
 ELSE
  BCNT%=MAINCNT+3
 ENDIF

IF (B% AND #Y)!=0 THEN
 SCALE#=5
 FOR I%=0 TO 15
  SPROT I%,0
  SPOFS I%,200,120
  SPSCALE I%,5,5
 NEXT
 SPHIDE 16
ENDIF
IF (B% AND #UP)!=0 THEN
 SPSCALE 0 OUT SX#,SY#
 IF SX#<10 THEN
  SX#=SX#+0.5
  FOR I%=0 TO 15
   SPSCALE I%,SX#,SX#
  NEXT
 ENDIF
ENDIF
IF (B% AND #DOWN)!=0 THEN
 SPSCALE 0 OUT SX#,SY#
 IF SX#>0.5 THEN
  SX#=SX#-0.5
  FOR I%=0 TO 15
   SPSCALE I%,SX#,SX#
  NEXT
 ENDIF
ENDIF
IF (B% AND #X)!=0 THEN
 SPCHR 16 OUT ,,,,A%
 IF (A%AND 1) THEN
  SPHIDE 16
 ELSE
  SPSHOW 16
 ENDIF
ENDIF
IF (B% AND #L)!=0 THEN
 SPROT 0 OUT R:R=(R+358)MOD 360
 FOR I%=0 TO 15
  SPROT I%,R
 NEXT
ENDIF
IF (B% AND #R)!=0 THEN
 SPROT 0 OUT R:R=(R+2)MOD 360
 FOR I%=0 TO 15
  SPROT I%,R
 NEXT
ENDIF
ENDIF

STICK OUT X#,Y#
IF ABS(X#)<0.1 && ABS(Y#)<0.1 THEN
 STCNT%=-1
ELSEIF MAINCNT>STCNT% THEN
 IF STCNT%<0 THEN
  STCNT%=MAINCNT+30
 ELSE
  STCNT%=MAINCNT+3
 ENDIF

 SPOFS 0 OUT SX%,SY%
 SX%=SX%+X#*10
 SY%=SY%-Y#*10
 IF SX%<0 THEN SX%=0
 IF SY%<0 THEN SY%=0
 IF SX%>400 THEN SX%=400
 IF SY%>240 THEN SY%=240
 FOR I%=0 TO 15
  SPOFS I%,SX%,SY%
 NEXT
ENDIF

UNTIL FALSE

@OUTRO
REPEAT
 'PINFO
 IF MVC%>=VC% THEN BREAK
 GOSUB @ROTVBUF
UNTIL FALSE
GOTO @TITLE
END
'''''''''''''''''''''''''''''''''

'ROTATE VIDEO BUFFER
@ROTVBUF
'PINFO
 IF MAINCNT>=VNCNT% THEN
  NVPP%=VPP%+1:IF NVPP%>=16 THEN NVPP%=0
  SPSHOW NVPP%
  SPHIDE VPP%
  VPP%=NVPP%
  VNCNT%=VNCNT%+4
  MVC%=MVC%+1
 ENDIF
RETURN

'DECODE I-FRAME
@IFRAMEDEC
 J%=0:I%=DC%+1
 WHILE J%<3840
  V%=D%[I%]
  C%=((V%<< 4) AND &HF000)OR((V%<< 3) AND &H780)OR((V%<< 2) AND &H3C)OR 1
  IF (V% AND &HF000) THEN
   L%=((V%>>12) AND &HF)+1
   FILL G%,C%,J%,L%
   J%=J%+L%
  ELSE
   G%[J%]=C%
   J%=J%+1
  ENDIF
  C%=((V%>>12) AND &HF000)OR((V%>>13) AND &H780)OR((V%>>14) AND &H3C)OR 1
  IF (V% AND &HF0000000) THEN
   L%=((V%>>28) AND &HF)+1
   FILL G%,C%,J%,L%
   J%=J%+L%
  ELSE
   G%[J%]=C%
   J%=J%+1
  ENDIF
  I%=I%+1
 WEND
RETURN

'DECODE P-FRAME
@PFRAMEDEC
 J%=0:I%=DC%+1
 WHILE J%<3840
  V%=D%[I%]
  IF (V% AND &H8000) THEN
   J%=J%+(V% AND &H7FFF)+1
  ELSE
   C%=((V%<< 4) AND &HF000)OR((V%<< 3) AND &H780)OR((V%<< 2) AND &H3C)OR 1
   IF (V% AND &H7000) THEN
    L%=((V%>>12) AND 7)+1
    FILL G%,C%,J%,L%
    J%=J%+L%
   ELSE
    G%[J%]=C%
    J%=J%+1
   ENDIF
  ENDIF
  IF (V% AND &H80000000) THEN
   J%=J%+((V%>>16) AND &H7FFF)+1
  ELSE
   C%=((V%>>12) AND &HF000)OR((V%>>13) AND &H780)OR((V%>>14) AND &H3C)OR 1
   IF (V% AND &H70000000) THEN
    L%=((V%>>28) AND 7)+1
    FILL G%,C%,J%,L%
    J%=J%+L%
   ELSE
    G%[J%]=C%
    J%=J%+1
   ENDIF
  ENDIF
  I%=I%+1
 WEND
RETURN

'DECODE ADPCM
@ADPCM2DEC_PCM
 I%=DC%+1:JJ%=0
 REPEAT
 MJ%=J%+2048
 REPEAT
  VV%=D%[I%]
  K%=0
  REPEAT
   V%=(VV%>>K%) AND 3
   IF V%==0 THEN
    PD%=PD%+(SA%>>1)
    IF PD%>32639 THEN PD%=32639
    SA%=(SA%*505+49)>>9
   ELSEIF V%==1 THEN
    PD%=PD%+SA%+(SA%>>1)
    IF PD%>32639 THEN PD%=32639
    SA%=(SA%*528+400)>>9
   ELSEIF V%==2 THEN
    PD%=PD%-(SA%>>1)
    IF PD%<-32640 THEN PD%=-32640
    SA%=(SA%*505+49)>>9
   ELSE
    PD%=PD%-SA%-(SA%>>1)
    IF PD%<-32640 THEN PD%=-32640
    SA%=(SA%*528+400)>>9
   ENDIF
   W%[J%]=PD%
   J%=J%+1
   K%=K%+2
  UNTIL K%==32
  I%=I%+1
 UNTIL J%>=MJ%
 GOSUB @ROTVBUF
 JJ%=JJ%+1
 UNTIL JJ%>=8
RETURN

@ADPCM2DEC
 I%=DC%+1:J%=0:JJ%=0
 REPEAT
 MJ%=J%+2048
 REPEAT
  VV%=D%[I%]
  K%=0
  REPEAT
   V%=(VV%>>K%) AND 3
   IF V%==0 THEN
    PD%=PD%+(SA%>>1)
    IF PD%>32639 THEN PD%=32639
    SA%=(SA%*505+49)>>9
   ELSEIF V%==1 THEN
    PD%=PD%+SA%+(SA%>>1)
    IF PD%>32639 THEN PD%=32639
    SA%=(SA%*528+400)>>9
   ELSEIF V%==2 THEN
    PD%=PD%-(SA%>>1)
    IF PD%<-32640 THEN PD%=-32640
    SA%=(SA%*505+49)>>9
   ELSE
    PD%=PD%-SA%-(SA%>>1)
    IF PD%<-32640 THEN PD%=-32640
    SA%=(SA%*528+400)>>9
   ENDIF
   W%[J%]=(PD%+&H7F80+RND(256))>>8
   J%=J%+1
   K%=K%+2
  UNTIL K%==32
  I%=I%+1
 UNTIL J%>=MJ%
 GOSUB @ROTVBUF
 JJ%=JJ%+1
 UNTIL JJ%>=8
 WAVSETA 224+SPP%,127,127,127,127,W%,69,0,WL%
RETURN

'FOR DEBUG
DEF PINFO
 LOCATE 0,10
 PRINT STR$(MAINCNT)+"  "
 PRINT F$+"  "
 PRINT STR$(CT%),STR$(CSIZE%)
 PRINT STR$(SNCNT%-MAINCNT,2),STR$(SNCNT%)+" "*10
 PRINT STR$(VNCNT%-MAINCNT,2),STR$(VNCNT%)+" *10
 PRINT STR$(SPP%,2)+"  "
 PRINT STR$(VC%-MVC%,2),STR$(VC%,2),STR$(MVC%,2)
 IF SKIP% THEN
  COLOR 3:PRINT "X"
  COLOR 15
 ELSE
  PRINT " "
 ENDIF
 PRINT STR$(I2%)+"  "
END

''''''
'SELECT UTIL FROM STRING LIST
DEF SELECT(L$[],NOMES$)
 VAR CY%=CSRY+1
 VAR B%=BUTTON(1),SCNT%
 VAR CI%=0,CI2%=0
 VAR I%,X#,Y#
 IF LEN(L$)==0 THEN
  LOCATE 0,CY%:COLOR 15,3
  PRINT NOMES$;
  COLOR 15,0
  WHILE TRUE
   VSYNC 1
   B%=BUTTON(1)
   IF B% AND #B THEN RETURN -1
  WEND
 ENDIF
 SCNT%=-1
 WHILE TRUE
  LOCATE 0,CY%-1
  IF CI2%>0 THEN PRINT "ðŽ‹¢" ELSE PRINT " "
  FOR I%=0 TO MIN(LEN(L$)-CI2%-1,28-CY%)
   IF I%==CI% THEN COLOR 15,3
   PRINT L$[I%+CI2%];
   COLOR 15,0
   PRINT " "*(50-CSRX);
  NEXT
  FOR I%=I% TO 28-CY%
   PRINT " "*50;
  NEXT
  LOCATE 0,29
  IF LEN(L$)-CI2%-1>28-CY% THEN 
   PRINT "ðŽ‹£";
  ELSE
   PRINT " ";
  ENDIF
  WHILE TRUE
   VSYNC 1
   B%=BUTTON(1)
   IF B% AND #UP   && CI%+CI2%>0 THEN
    IF CI% >0 THEN DEC CI% ELSE DEC CI2%
    BREAK
   ENDIF
   IF B% AND #DOWN && CI%+CI2%+1<LEN(L$) THEN
    IF CI%+CY%<28 THEN INC CI% ELSE INC CI2%
    BREAK
   ENDIF
   IF B% AND #B THEN RETURN -1
   IF B%==#A THEN RETURN CI%+CI2%
   STICK OUT X#,Y#
   IF Y#<-0.5 THEN
    IF MAINCNT>SCNT% THEN
     IF SCNT%<0 THEN
      SCNT%=MAINCNT+30
     ELSE
      SCNT%=MAINCNT+3
     ENDIF
     CI2%=CI2%+(28-CY%)+1
     IF CI2%>LEN(L$)-1-(28-CY%) THEN
      CI2%=MAX(LEN(L$)-1-(28-CY%),0)
      CI%=MIN(28-CY%,LEN(L$)-1-CI2%)
     ENDIF
     BREAK
    ENDIF
   ELSEIF Y#>0.5 THEN
    IF MAINCNT>SCNT% THEN
     IF SCNT%<0 THEN
      SCNT%=MAINCNT+30
     ELSE
      SCNT%=MAINCNT+3
     ENDIF
     CI2%=CI2%-(28-CY%)-1
     IF CI2%<0 THEN
      CI2%=0
      CI%=0
     ENDIF
     BREAK
    ENDIF
   ELSE
    SCNT%=-1
   ENDIF
  WEND
 WEND
END

'''''''''''''''''''
CNT%=MAINCNT+1
WHILE MAINCNT<CNT%
WEND
FOR I%=0 TO 10000
CLS
NEXT
?MAINCNT-CNT%

